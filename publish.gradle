apply plugin: 'maven-publish'
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.fulvmei.android'
                artifactId = findProperty('releaseArtifactId') ?: ''
                version = findProperty('releaseVersion') ?: ''
                configurations.create("sourcesElement") { variant ->
                    variant.visible = false
                    variant.canBeResolved = false
                    variant.attributes.attribute(
                            Usage.USAGE_ATTRIBUTE,
                            project.objects.named(Usage, Usage.JAVA_RUNTIME))
                    variant.attributes.attribute(
                            Category.CATEGORY_ATTRIBUTE,
                            project.objects.named(Category, Category.DOCUMENTATION))
                    variant.attributes.attribute(
                            Bundling.BUNDLING_ATTRIBUTE,
                            project.objects.named(Bundling, Bundling.EXTERNAL))
                    variant.attributes.attribute(
                            DocsType.DOCS_TYPE_ATTRIBUTE,
                            project.objects.named(DocsType, DocsType.SOURCES))
                    variant.outgoing.artifact(androidSourcesJar)
                    components.release.addVariantsFromConfiguration(variant) {}
                }
                pom {
                    name =
                            findProperty('releaseName')
                                    ?: ''
                    description =
                            findProperty('releaseDescription')
                                    ?: findProperty('releaseName')
                                    ?: ''
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'fulvmei'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/fulvmei/Media.git'
                        url = 'https://github.com/fulvmei/Media'
                    }
//                withXml {
//                    addMissingAarTypeToXml(it)
//                }
                }
            }
        }
    }
}
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}